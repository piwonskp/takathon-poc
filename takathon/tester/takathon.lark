start: "test:" _NEWLINE _INDENT (_stmt | domain_stmt)+ _DEDENT

import_stmt: IMPORT_STMT _NEWLINE
mock_stmt: MOCK_STMT _NEWLINE
domain_stmt: DOMAIN_STMT (fun_call | _NEWLINE+ _INDENT _stmt* fun_call _DEDENT)

_stmt: mock_stmt | import_stmt

fun_call: _results_stmt | _result_by_function_stmt | _throws_stmt
_results_stmt: RESULTS_STMT [_NEWLINE]
_result_by_function_stmt: RESULT_BY_FUNCTION_STMT [_NEWLINE]
_throws_stmt: THROWS_STMT [_NEWLINE]

IMPORT_STMT: /(from .+? )?import .+/
MOCK_STMT: /mock .+? as .+/
DOMAIN_STMT: /domain .*?:/

RESULTS_STMT: /results .+/
RESULT_BY_FUNCTION_STMT: /result .+/
THROWS_STMT: /throws .+/

COMMENT: /#[^\n]*/
_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+

%import common.WS_INLINE
%ignore WS_INLINE
%ignore COMMENT
%declare _INDENT _DEDENT
